prototype(M12.Foundation:Orbit) < prototype(M12.Foundation:Content) {
	templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/Orbit.html'
	sectionName = 'sliderRoot'

	node = ${node}

	attributes.data-orbit = ''
	attributes.data-options = TYPO3.TypoScript:RawArray {
		animation = ${q(node).property('animation')                 ? 'animation:' + q(node).property('setAnimation') + ';' : null}
		animationSpeed = ${q(node).property('animationSpeed')       ? 'animation_speed:' + String.toInteger(q(node).property('animationSpeed')) + ';' : null}
		timerSpeed = ${q(node).property('timerSpeed')               ? 'timer_speed:' + String.toInteger(q(node).property('timerSpeed')) + ';' : null}
		bullets = ${q(node).property('bullets')                     ? 'bullets:true;' : 'bullets:false;'}
		navigationArrows = ${q(node).property('navigationArrows')   ? 'navigation_arrows:true;' : 'navigation_arrows:false;'}
		nextOnClick = ${q(node).property('nextOnClick')             ? 'next_on_click:true;' : 'next_on_click:false;'}
		slideNumber = ${q(node).property('slideNumber')             ? 'slide_number:true;' : 'slide_number:false;'}
		timer = ${q(node).property('timer')                         ? 'timer:true;' : 'timer:false;'}
		variableHeight = ${q(node).property('variableHeight')       ? 'variable_height:true;' : 'variable_height:false;'}
		swipe = ${q(node).property('swipe')                         ? 'swipe:true;' : 'swipe:false;'}
		pauseOnHover = ${q(node).property('pauseOnHover')           ? 'pause_on_hover:true;' : 'pause_on_hover:false;'}
		resumeOnMouseout = ${q(node).property('resumeOnMouseout')   ? 'resume_on_mouseout:true;' : 'resume_on_mouseout:false;'}
	}

	@override.parentAttributes = ${this.attributes}

	orbitContent = TYPO3.Neos:ContentCollection {
		nodePath = 'content'

		iterationName = 'iterator'
		itemName = 'orbitNodeItem'
		itemRenderer = M12.Foundation:OrbitItem
	}

	@process.wrapRemover = M12.Utils:WrapRemover
	@process.wrapRemover.wrapperClass = 'extra-wrapper-required-by-neos-backend'
}


prototype(M12.Foundation:OrbitItem) < prototype(M12.Foundation:Content) {
	templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/Orbit.html'
	sectionName = 'sliderItem'

	iterator = ${iterator}
	parentAttributes = ${parentAttributes}

	orbitItemContent = TYPO3.TypoScript:Collection {
		collection = ${q(orbitNodeItem)}
		itemName = 'node'
		itemRenderer = TYPO3.Neos:ContentCase
	}

	#
	# Re-define how Image is rendered inside slider
	#
	prototype(TYPO3.Neos.NodeTypes:Image) {
		templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/Orbit.html'
		sectionName = 'sliderItemImage'
    }
}
