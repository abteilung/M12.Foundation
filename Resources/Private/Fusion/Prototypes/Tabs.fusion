prototype(M12.Foundation:Tabs) < prototype(M12.Foundation:Content) {
	templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/Tabs.html'
	sectionName = 'tabs'

	tabsNavContent = Neos.Fusion:Tag {
		tagName = 'ul'
		attributes.data-tabs = ''
		attributes.id = ${q(node).property('customHtmlId') ? q(node).property('customHtmlId') : ('tab-' + q(node).property('_identifier'))}
		attributes.class = Neos.Fusion:RawArray {
			base = 'tabs'
			vertical = ${q(node).property('vertical') ? ' vertical' : null}
		}
		content = Neos.Fusion:Collection {
			collection = ${q(node).children('[instanceof M12.Foundation:TabItem]')}
			itemRenderer = M12.Foundation:TabNavItem
			itemName = 'node'
		}
	}

	# Attributes around div.tabs-content
	extraAttributes.class.base = 'tabs-content'
	extraAttributes.data-tabs-content = ${q(node).property('customHtmlId') ? q(node).property('customHtmlId') : ('tab-' + q(node).property('_identifier'))}
	extraAttributes.class.vertical = ${q(node).property('vertical') ? 'vertical' : null}

	tabsContent = Neos.Fusion:Collection {
		collection = ${q(node).children('[instanceof M12.Foundation:TabItem]')}
		itemRenderer = M12.Foundation:TabContentItem
		itemName = 'node'
	}

	@process.wrapRemover = M12.Utils:WrapRemover
	@process.wrapRemover.wrapperClass = 'm12-foundation-tabs'
}

prototype(M12.Foundation:TabNavItem) < prototype(M12.Foundation:Content) {
	templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/Tabs.html'
	sectionName = 'tabNavItem'

	# Attributes for each LI element
	attributes.class {
		tabTitle = 'tabs-title'
		active = ${q(node).property('activeTab') ? 'is-active' : null}
	}

	title = ${q(node).property('title')}
	href = ${q(node).property('customHtmlId') ? q(node).property('customHtmlId') : ('tab-' + q(node).property('_identifier'))}

	# Unset contentElementWrapping, as tabNavItems don't need it
	@process.contentElementWrapping >
}

# Abstract render definition for a grid column
prototype(M12.Foundation:TabContentItem) < prototype(M12.Foundation:Content) {
	templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/Tabs.html'
	sectionName = 'tabContentItem'

	attributes.class {
		contentcollection = 'neos-contentcollection'
		contentcollection.@if.isBackend = ${node.context.workspace.name != 'live'}
		base = 'tabs-panel'
		active = ${q(node).property('activeTab') ? 'is-active' : null}
	}
	attributes.id = ${q(node).property('customHtmlId') ? q(node).property('customHtmlId') : ('tab-' + q(node).property('_identifier'))}

	content = Neos.Neos:ContentCollectionRenderer
}
