prototype(M12.Foundation:BlockGrid) < prototype(M12.Foundation:Content) {
	templatePath = 'resource://M12.Foundation/Private/Templates/NodeTypes/BlockGrid.html'
	node = ${node}

	attributes.class {
		contentcollection = 'neos-contentcollection'
		contentcollection.@if.isBackend = ${node.context.workspace.name != 'live'}
		itemsPerRow = ${q(node).property('classGridItemsPerRow') ? String.trim(Array.join(q(node).property('classGridItemsPerRow'), ' ')) : null}
		clearing-thumbs = ${q(node).property('lightboxEnabled') && node.context.workspace.name == 'live' ? 'clearing-thumbs' : null}
	}
	attributes.data-clearing = ${q(node).property('lightboxEnabled') && node.context.workspace.name == 'live' ? '' : null}
	@override.attributes = ${this.attributes}

	blockGridContent = Neos.Fusion:Tag {
		tagName = 'ul'
		attributes = ${attributes}

		content = Neos.Neos:ContentCollectionRenderer {
			iterationName = 'iterator'
			itemName = 'node'
			itemRenderer = M12.Foundation:BlockGridItem
		}

		# Add additional wrapping to outer DIV, because otherwise
		# CreateJS won't work. It requires elements of ContentCollection
		# to be direct children of ContentCollection).
		# TODO: Can be removed when createjs is dropped.
		@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
	}

	@process.wrapRemover = M12.Utils:WrapRemover
	@process.wrapRemover.wrapperClass = 'extra-wrapper-required-by-neos-backend'
}

prototype(M12.Foundation:BlockGridItem) < prototype(Neos.Fusion:Tag) {
	tagName = 'li'

	content = Neos.Neos:ContentCase
	@process.contentElementWrapping = Neos.Neos:ContentElementWrapping
}
